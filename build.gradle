plugins {
    id 'base'
    id 'org.jetbrains.kotlin.jvm' version '2.2.0'
    id 'org.jetbrains.kotlin.kapt' version '2.2.0'
    id "com.vanniktech.maven.publish" version "0.34.0"
    id "org.jlleitschuh.gradle.ktlint" version "13.1.0" apply false
    id 'com.diffplug.spotless' version "7.2.1" apply false
}

allprojects {
    group = findProperty('group') as String
    if (hasProperty("version")) {
        version = findProperty('version') as String
    } else {
        version = findProperty('baseVersion') as String
    }
    repositories {
        mavenCentral()
    }
}

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'org.jetbrains.kotlin.jvm'
    apply plugin: 'org.jetbrains.kotlin.kapt'
    apply plugin: 'com.vanniktech.maven.publish'
    apply plugin: 'org.jlleitschuh.gradle.ktlint'
    apply plugin: 'com.diffplug.spotless'

    // Configure Vanniktech plugin
    mavenPublishing {
        publishToMavenCentral(true)
        signAllPublications()

        coordinates(rootProject.group as String,
                "${rootProject.name}-${project.name}",
                rootProject.version as String)

        pom {
            name = "${rootProject.name}-${project.name}"
            description = "A module of the ${rootProject.name} project."
            url = "https://github.com/EventHorizonLab/SPI-Tooling"
            licenses {
                license {
                    name = "MIT License"
                    url = "https://opensource.org/license/mit"
                }
            }
            developers {
                developer {
                    id = "eventhorizonlab"
                    name = "EventHorizonLab"
                    url = "https://github.com/EventHorizonLab"
                }
            }
            scm {
                url = "https://github.com/EventHorizonLab/SPI-Tooling"
                connection = "scm:git:https://github.com/EventHorizonLab/SPI-Tooling.git"
                developerConnection = "scm:git:ssh://git@github.com:EventHorizonLab/SPI-Tooling.git"
            }
        }
    }

    // Add GitHub Packages as an extra target
    publishing {
        repositories {
            maven {
                name = "GitHubPackages"
                url = uri("https://maven.pkg.github.com/EventHorizonLab/SPI-Tooling")
                credentials {
                    username = findProperty('gprUser') ?: System.getenv('GITHUB_ACTOR')
                    password = findProperty('gprToken') ?: System.getenv('GITHUB_TOKEN')
                }
            }
        }
    }

    tasks.named("check") {
        dependsOn("spotlessApply")
    }

    tasks.named("compileKotlin") {
        dependsOn(tasks.named("spotlessApply"))
    }

    spotless {
        ratchetFrom 'origin/main'

        format 'misc', {
            target '*.gradle', '.gitattributes', '.gitignore'

            trimTrailingWhitespace()
            leadingTabsToSpaces()
            endWithNewline()
        }

        kotlin {
            ktlint().setEditorConfigPath("$rootDir/.editorconfig")
        }
    }
}

tasks.register('copyJar', Copy) {
    description = 'Collects all subproject jars into the root build/ directory'
    group = 'build'
    dependsOn subprojects.collect { it.tasks.matching { it.name == 'jar' } }
    from subprojects.collect { proj -> proj.tasks.named('jar').flatMap { it.archiveFile } }
    into layout.buildDirectory.dir('libs')
}
